**Chapter 03 [Do Things: A Clojure Crash Course](https://www.braveclojure.com/do-things/)**


<!-- vim-markdown-toc GFM -->

* [Syntax](#syntax)
    * [Forms](#forms)
    * [Control Flow](#control-flow)
        * [if](#if)
        * [do](#do)
        * [when](#when)
        * [nil, true, false](#nil-true-false)
        * [Truthiness](#truthiness)
        * [Equality](#equality)
        * [Boolean](#boolean)
    * [Naming Values with def](#naming-values-with-def)
* [Data Structures](#data-structures)
    * [Numbers](#numbers)
    * [Strings](#strings)
    * [Maps](#maps)
    * [Keywords](#keywords)
    * [Vectors](#vectors)
    * [Lists](#lists)
    * [Sets](#sets)
    * [Simplicity](#simplicity)
* [Functions](#functions)
    * [Calling Functions](#calling-functions)
    * [Function Calls, Macro Calls, and Special Forms](#function-calls-macro-calls-and-special-forms)
    * [Defining Functions](#defining-functions)
        * [The Docstring](#the-docstring)
        * [Parameters and Arity](#parameters-and-arity)
        * [Destructuring](#destructuring)
        * [Function Body](#function-body)
        * [All Functions Are Created Equal](#all-functions-are-created-equal)
    * [Anonymous Functions](#anonymous-functions)
    * [Returning Functions](#returning-functions)
* [Pulling It All Together](#pulling-it-all-together)
    * [The Shire’s Next Top Model](#the-shires-next-top-model)
    * [let](#let)
    * [loop](#loop)
    * [Regular Expressions](#regular-expressions)
    * [Symmetrizer](#symmetrizer)
    * [Better Symmetrizer with reduce](#better-symmetrizer-with-reduce)
    * [Hobbit Violence](#hobbit-violence)
* [Summary](#summary)
* [Exercises](#exercises)

<!-- vim-markdown-toc -->

# Syntax

Clojure’s syntax is simple. Like all Lisps, it employs a uniform structure, a handful of special operators, and a constant supply of parentheses delivered from the parenthesis mines hidden beneath the Massachusetts Institute of Technology, where Lisp was born.

## Forms

All Clojure code is written in a uniform structure. Clojure recognizes two kinds of structures:
- Literal representations of data structures (like numbers, strings, maps, and vectors)
- Operations

Clojure *evaluates* every form to produce a value. These literal representations are all valid forms:
```clojure
1
"a string"
["a" "vector" "of" "strings"]
```
```clojure
(+ 1 2 3)
; => 6

(str "It was the panda " "in the library " "with a dust buster")
; => "It was the panda in the library with a dust buster"
```

## Control Flow
### if
This is the general structure for an `if` expression:
```
(if boolean-form
  then-form
  optional-else-form)
```

Examples:
```clojure
(if true
  "By Zeus's hammer!"
  "By Aquaman's trident!")
; => "By Zeus's hammer!"

(if false
  "By Zeus's hammer!"
  "By Aquaman's trident!")
; => "By Aquaman's trident!"

(if false
  "By Odin's Elbow!")
; => nil
```
### do
```clojure
(if true
  (do (println "Success!")
      "By Zeus's hammer!")
  (do (println "Failure!")
      "By Aquaman's trident!"))
; => Success!
; => "By Zeus's hammer!"
```

### when
The `when` operator is like a combination of `if` and `do`, but with no `else` branch. Here’s an example:
```clojure
(when true
  (println "Success!")
  "abra cadabra")
; => Success!
; => "abra cadabra"
```

### nil, true, false
Clojure has `true` and `false` values. `nil` is used to indicate no value in Clojure. You can check if a value is `nil` with the appropriately named `nil`? function:
```clojure
(nil? 1)
; => false

(nil? nil)
; => true
```
### Truthiness
```clojure
(if "bears eat beets"
  "bears beets Battlestar Galactica")
; => "bears beets Battlestar Galactica"

(if nil
  "This won't be the result because nil is falsey"
  "nil is falsey")
; => "nil is falsey"
```
### Equality
Clojure’s equality operator is `=`:
```clojure
(= 1 1)
; => true

(= nil nil)
; => true

(= 1 2)
; => false
```
### Boolean
```clojure
(or false nil :large_I_mean_venti :why_cant_I_just_say_large)
; => :large_I_mean_venti

(or (= 0 1) (= "yes" "no"))
; => false

(or nil)
; => nil
```
```clojure
(and :free_wifi :hot_coffee)
; => :hot_coffee

(and :hot_coffee :free_wifi)
; => :free_wifi

(and :feelin_super_cool nil false)
; => nil

(and :feelin_super_cool false nil)
; => false
```

## Naming Values with def

```clojure
(def failed-protagonist-names
  ["Larry Potter" "Doreen the Explorer" "The Incredible Bulk"])

failed-protagonist-names
; => ["Larry Potter" "Doreen the Explorer" "The Incredible Bulk"]
```

For example, in Ruby you might perform multiple assignments to a variable to build up its value:
```ruby
severity = :mild
error_message = "OH GOD! IT'S A DISASTER! WE'RE "
if severity == :mild
  error_message = error_message + "MILDLY INCONVENIENCED!"
else
  error_message = error_message + "DOOOOOOOMED!"
end
```
You might be tempted to do something similar in Clojure:
```clojure
(def severity :mild)
(def error-message "OH GOD! IT'S A DISASTER! WE'RE ")
(if (= severity :mild)
  (def error-message (str error-message "MILDLY INCONVENIENCED!"))
  (def error-message (str error-message "DOOOOOOOMED!")))
```
As you learn Clojure, you’ll find that you’ll rarely need to alter a name/value association. Here’s one way you could write the preceding code:
```clojure
(defn error-message
  [severity]
  (str "OH GOD! IT'S A DISASTER! WE'RE "
       (if (= severity :mild)
         "MILDLY INCONVENIENCED!"
         "DOOOOOOOMED!")))

(error-message :mild)
; => "OH GOD! IT'S A DISASTER! WE'RE MILDLY INCONVENIENCED!"
```
Here, you create a function, `error-message`, which accepts a single argument, `severity`, and uses that to determine which string to return. You then call the function with `:mild` for the severity.

# Data Structures
All of Clojure’s data structures are immutable, meaning you can’t change them in place. For example, in Ruby you could do the following to reassign the failed protagonist name at index 0:
```ruby
failed_protagonist_names = [
  "Larry Potter",
  "Doreen the Explorer",
  "The Incredible Bulk"
]
failed_protagonist_names[0] = "Gary Potter"

failed_protagonist_names
# => [
#   "Gary Potter",
#   "Doreen the Explorer",
#   "The Incredible Bulk"
# ]
```
Clojure has no equivalent for this.

## Numbers
```clojure
93
1.2
1/5
```
## Strings

```clojure
"Lord Voldemort"
"\"He who must not be named\""
"\"Great cow of Moscow!\" - Hermes Conrad"
```

```clojure
(def name "Chewbacca")
(str "\"Uggllglglglglglglglll\" - " name)
; => "Uggllglglglglglglglll" - Chewbacca
```
## Maps
```clojure
{}

{:first-name "Charlie"
 :last-name "McFishwich"}

{"string-key" +}

{:name {:first "John" :middle "Jacob" :last "Jingleheimerschmidt"}}
```
Besides using map literals, you can use the `hash-map` function to create a map:
```clojure
(hash-map :a 1 :b 2)
; => {:a 1 :b 2}
```
You can look up values in maps with the `get` function:
```clojure
(get {:a 0 :b 1} :b)
; => 1

(get {:a 0 :b {:c "ho hum"}} :b)
; => {:c "ho hum"}
```
`get` will return `nil` if it doesn’t find your key, or you can give it a default value to return, such as `"unicorns?"`:
```clojure
(get {:a 0 :b 1} :c)
; => nil

(get {:a 0 :b 1} :c "unicorns?")
; => "unicorns?"
```

The `get-in` function lets you look up values in nested maps:
```clojure
(get-in {:a 0 :b {:c "ho hum"}} [:b :c])
; => "ho hum"
```


Another way to look up a value in a map is to treat the map like a function with the key as its argument:

```clojure
({:name "The Human Coffeepot"} :name)
; => "The Human Coffeepot"
```

## Keywords
Clojure keywords are best understood by seeing how they’re used. They’re primarily used as keys in maps, as you saw in the preceding section. Here are some more examples of keywords:
```clojure
:a
:rumplestiltsken
:34
:_?
```
Keywords can be used as functions that look up the corresponding value in a data structure. For example, you can look up `:a` in a map:
```clojure
(:a {:a 1 :b 2 :c 3})
; => 1
```
This is equivalent to:

```clojure
(get {:a 1 :b 2 :c 3} :a)
; => 1
```
You can provide a default value, as with `get`:
```clojure
(:d {:a 1 :b 2 :c 3} "No gnome knows homes like Noah knows")
; => "No gnome knows homes like Noah knows"
```
## Vectors
```clojure
[3 2 1]
(get [3 2 1] 0)
; => 3

(get ["a" {:name "Pugsley Winterbottom"} "c"] 1)
; => {:name "Pugsley Winterbottom"}
```
You can create vectors with the `vector` function:
```clojure
(vector "creepy" "full" "moon")
; => ["creepy" "full" "moon"]
```
You can use the `conj` function to add additional elements to the vector. Elements are added to the end of a vector:
```clojure
(conj [1 2 3] 4)
; => [1 2 3 4]
```
## Lists
Lists are similar to vectors in that they’re linear collections of values. But there are some differences. For example, you can’t retrieve list elements with `get`. To write a list literal, just insert the elements into parentheses and use a single quote at the beginning:
```clojure
'(1 2 3 4)
; => (1 2 3 4)

(nth '(:a :b :c) 0)
; => :a

(nth '(:a :b :c) 2)
; => :c
```
```clojure
(list 1 "two" {3 4})
; => (1 "two" {3 4})

(conj '(1 2 3) 4)
; => (4 1 2 3)
```
## Sets
```clojure
#{"kurt vonnegut" 20 :icicle}

(hash-set 1 1 2 2)
; => #{1 2}
```
```clojure
(conj #{:a :b} :b)
; => #{:a :b}

(set [3 3 3 4 4])
; => #{3 4}
```

```clojure
(contains? #{:a :b} :a)
; => true

(contains? #{:a :b} 3)
; => false

(contains? #{nil} nil)
; => true
```
```clojure
(:a #{:a :b})
; => :a

(get #{:a :b} :a)
; => :a

(get #{:a nil} nil)
; => nil

(get #{:a :b} "kurt vonnegut")
; => nil
```
## Simplicity
# Functions

This section initiates you into the beauty and elegance of Lisp functions by explaining the following:
- Calling functions
- How functions differ from macros and special forms
- Defining functions
- Anonymous functions
- Returning functions

## Calling Functions
```clojure
(+ 1 2 3 4)
(* 1 2 3 4)
(first [1 2 3 4])
```

```clojure
(or + -)
; => #<core$_PLUS_ clojure.core$_PLUS_@76dace31>
```

```clojure
((and (= 1 1) +) 1 2 3)
; => 6

((first [+ 0]) 1 2 3)
; => 6
```
Take the `map` function (not to be confused with the map data structure), for instance. `map` creates a new list by applying a function to each member of a collection. Here, the `inc` function increments a number by 1:
```clojure
(inc 1.1)
; => 2.1

(map inc [0 1 2 3])
; => (1 2 3 4)
```
Clojure’s support for first-class functions allows you to build more power­ful abstractions than you can in languages without them.


Here’s how Clojure would evaluate a function call whose arguments are also function calls:
```clojure
(+ (inc 199) (/ 100 (- 7 2)))
(+ 200 (/ 100 (- 7 2))) ; evaluated "(inc 199)"
(+ 200 (/ 100 5)) ; evaluated (- 7 2)
(+ 200 20) ; evaluated (/ 100 5)
220 ; final evaluation
```

## Function Calls, Macro Calls, and Special Forms

```clojure
(if good-mood
  (tweet walking-on-sunshine-lyrics)
  (tweet mopey-country-song-lyrics))
```
## Defining Functions

Function definitions are composed of five main parts:
- `defn`
- Function name
- A docstring describing the function (optional)
- Parameters listed in brackets
- Function body
```clojure
(defn too-enthusiastic
   "Return a cheer that might be a bit too enthusiastic"
   [name]
   (str "OH. MY. GOD! " name " YOU ARE MOST DEFINITELY LIKE THE BEST "
  "MAN SLASH WOMAN EVER I LOVE YOU AND WE SHOULD RUN AWAY SOMEWHERE"))

(too-enthusiastic "Zelda")
; => "OH. MY. GOD! Zelda YOU ARE MOST DEFINITELY LIKE THE BEST MAN SLASH WOMAN EVER I LOVE YOU AND WE SHOULD RUN AWAY SOMEWHERE"
```
### The Docstring
### Parameters and Arity

```clojure
(defn no-params
  []
  "I take no parameters!")
(defn one-param
  [x]
  (str "I take one parameter: " x))
(defn two-params
  [x y]
  (str "Two parameters! That's nothing! Pah! I will smoosh them "
  "together to spite you! " x y))
```
Functions also support arity overloading. This means that you can define a function so a different function body will run depending on the arity. Here’s the general form of a multiple-arity function definition. Notice that each arity definition is enclosed in parentheses and has an argument list:
```clojure
(defn multi-arity
  ;; 3-arity arguments and body
  ([first-arg second-arg third-arg]
     (do-things first-arg second-arg third-arg))
  ;; 2-arity arguments and body
  ([first-arg second-arg]
     (do-things first-arg second-arg))
  ;; 1-arity arguments and body
  ([first-arg]
     (do-things first-arg)))
```
Arity overloading is one way to provide default values for arguments.
```clojure
(defn x-chop
  "Describe the kind of chop you're inflicting on someone"
  ([name chop-type]
     (str "I " chop-type " chop " name "! Take that!"))
  ([name]
     (x-chop name "karate")))
```
```clojure
(x-chop "Kanye West" "slap")
; => "I slap chop Kanye West! Take that!"

(x-chop "Kanye East")
; => "I karate chop Kanye East! Take that!"
```
You can also make each arity do something completely unrelated:
```clojure
(defn weird-arity
  ([]
     "Destiny dressed you this morning, my friend, and now Fear is
     trying to pull off your pants. If you give up, if you give in,
     you're gonna end up naked with Fear just standing there laughing
     at your dangling unmentionables! - the Tick")
  ([number]
     (inc number)))
```
Clojure also allows you to define variable-arity functions by including a *rest parameter*. Indicated by `&`:

```clojure
(defn codger-communication
  [whippersnapper]
  (str "Get off my lawn, " whippersnapper "!!!"))

(defn codger
   [& whippersnappers]
  (map codger-communication whippersnappers))

(codger "Billy" "Anne-Marie" "The Incredible Bulk")
; => ("Get off my lawn, Billy!!!"
      "Get off my lawn, Anne-Marie!!!"
      "Get off my lawn, The Incredible Bulk!!!")
```
```clojure
(defn favorite-things
  [name & things]
  (str "Hi, " name ", here are my favorite things: "
       (clojure.string/join ", " things)))

(favorite-things "Doreen" "gum" "shoes" "kara-te")
; => "Hi, Doreen, here are my favorite things: gum, shoes, kara-te"
```
### Destructuring
The basic idea behind destructuring is that it lets you concisely bind names to values within a collection. Let’s look at a basic example:
```clojure
;; Return the first element of a collection
(defn my-first
  [[first-thing]] ; Notice that first-thing is within a vector
  first-thing)

(my-first ["oven" "bike" "war-axe"])
; => "oven"
```

```clojure
(defn chooser
  [[first-choice second-choice & unimportant-choices]]
  (println (str "Your first choice is: " first-choice))
  (println (str "Your second choice is: " second-choice))
  (println (str "We're ignoring the rest of your choices. "
                "Here they are in case you need to cry over them: "
                (clojure.string/join ", " unimportant-choices))))

(chooser ["Marmalade", "Handsome Jack", "Pigpen", "Aquaman"])
; => Your first choice is: Marmalade
; => Your second choice is: Handsome Jack
; => We're ignoring the rest of your choices. Here they are in case \
     you need to cry over them: Pigpen, Aquaman
```
You can also destructure maps. In the same way that you tell Clojure to destructure a vector or list by providing a vector as a parameter, you destructure maps by providing a map as a parameter:
```clojure
(defn announce-treasure-location
   [{lat :lat lng :lng}]
  (println (str "Treasure lat: " lat))
  (println (str "Treasure lng: " lng)))

(announce-treasure-location {:lat 28.22 :lng 81.33})
; => Treasure lat: 100
; => Treasure lng: 50
```
We often want to just break keywords out of a map, so there’s a shorter syntax for that. This has the same result as the previous example:

```clojure
(defn announce-treasure-location
  [{:keys [lat lng]}]
  (println (str "Treasure lat: " lat))
  (println (str "Treasure lng: " lng)))
```

You can retain access to the original map argument by using the `:as` keyword. In the following example, the original map is accessed with `treasure-location`:

```clojure
(defn receive-treasure-location
  [{:keys [lat lng] :as treasure-location}]
  (println (str "Treasure lat: " lat))
  (println (str "Treasure lng: " lng))

  ;; One would assume that this would put in new coordinates for your ship
  (steer-ship! treasure-location))
```
In general, you can think of destructuring as instructing Clojure on how to associate names with values in a list, map, set, or vector. Now, on to the part of the function that actually does something: the function body!

### Function Body
The function body can contain forms of any kind. Clojure automatically returns the last form evaluated.
```clojure
(defn illustrative-function
  []
  (+ 1 304)
  30
  "joe")

(illustrative-function)
; => "joe"
```
```clojure
(defn number-comment
  [x]
  (if (> x 6)
    "Oh my gosh! What a big number!"
    "That number's OK, I guess"))

(number-comment 5)
; => "That number's OK, I guess"

(number-comment 7)
; => "Oh my gosh! What a big number!"
```
### All Functions Are Created Equal
## Anonymous Functions
In Clojure, functions don’t need to have names. In fact, you’ll use *anonymous* functions all the time. How mysterious! You create anonymous functions in two ways. The first is to use the `fn` form:
```clojure
(fn [param-list]
  function body)
```
```clojure
(map (fn [name] (str "Hi, " name))
     ["Darth Vader" "Mr. Magoo"])
; => ("Hi, Darth Vader" "Hi, Mr. Magoo")

((fn [x] (* x 3)) 8)
; => 24
```
You can treat `fn` nearly identically to the way you treat `defn`.
```clojure
(def my-special-multiplier (fn [x] (* x 3)))
(my-special-multiplier 12)
; => 36
```
Clojure also offers another, more compact way to create anonymous functions. Here’s what an anonymous function looks like:

```clojure
#(* % 3)
```

```clojure
(#(* % 3) 8)
; => 24
```
```clojure
(map #(str "Hi, " %)
     ["Darth Vader" "Mr. Magoo"])
; => ("Hi, Darth Vader" "Hi, Mr. Magoo")
```
As you may have guessed by now, the percent sign, `%`, indicates the argument passed to the function. If your anonymous function takes multiple arguments, you can distinguish them like this: `%1`, `%2`, `%3`, and so on. `%` is equivalent to `%1`:
```clojure
(#(str %1 " and " %2) "cornbread" "butter beans")
; => "cornbread and butter beans"
```
You can also pass a rest parameter with `%&`:
```clojure
(#(identity %&) 1 "blarg" :yip)
; => (1 "blarg" :yip)
```
If you need to write a simple anonymous function, using this style is best because it’s visually compact. On the other hand, it can easily become unreadable if you’re writing a longer, more complex function. If that’s the case, use `fn`.

## Returning Functions
By now you’ve seen that functions can return other functions. The returned functions are *closures*, which means that they can access all the variables that were in scope when the function was created. Here’s a standard example:
```clojure
(defn inc-maker
  "Create a custom incrementor"
  [inc-by]
  #(+ % inc-by))

(def inc3 (inc-maker 3))

(inc3 7)
; => 10
```
# Pulling It All Together
## The Shire’s Next Top Model
```clojure
(def asym-hobbit-body-parts [{:name "head" :size 3}
                             {:name "left-eye" :size 1}
                             {:name "left-ear" :size 1}
                             {:name "mouth" :size 1}
                             {:name "nose" :size 1}
                             {:name "neck" :size 2}
                             {:name "left-shoulder" :size 3}
                             {:name "left-upper-arm" :size 3}
                             {:name "chest" :size 10}
                             {:name "back" :size 10}
                             {:name "left-forearm" :size 3}
                             {:name "abdomen" :size 6}
                             {:name "left-kidney" :size 1}
                             {:name "left-hand" :size 2}
                             {:name "left-knee" :size 2}
                             {:name "left-thigh" :size 4}
                             {:name "left-lower-leg" :size 3}
                             {:name "left-achilles" :size 1}
                             {:name "left-foot" :size 2}])
```
This is a vector of maps. Each map has the name of the body part and relative size of the body part.

Conspicuously missing is the hobbit’s right side. Let’s fix that.
```clojure
(defn matching-part
  [part]
  {:name (clojure.string/replace (:name part) #"^left-" "right-")
   :size (:size part)})

(defn symmetrize-body-parts
  "Expects a seq of maps that have a :name and :size"
  [asym-body-parts]
  (loop [remaining-asym-parts asym-body-parts
         final-body-parts []]
    (if (empty? remaining-asym-parts)
      final-body-parts
      (let [[part & remaining] remaining-asym-parts]
        (recur remaining
               (into final-body-parts
                     (set [part (matching-part part)])))))))
```
When we call the function `symmetrize-body-parts` on `asym-hobbit-body-parts`, we get a fully symmetrical hobbit:

```clojure
(symmetrize-body-parts asym-hobbit-body-parts)
; => [{:name "head", :size 3}
      {:name "left-eye", :size 1}
      {:name "right-eye", :size 1}
      {:name "left-ear", :size 1}
      {:name "right-ear", :size 1}
      {:name "mouth", :size 1}
      {:name "nose", :size 1}
      {:name "neck", :size 2}
      {:name "left-shoulder", :size 3}
      {:name "right-shoulder", :size 3}
      {:name "left-upper-arm", :size 3}
      {:name "right-upper-arm", :size 3}
      {:name "chest", :size 10}
      {:name "back", :size 10}
      {:name "left-forearm", :size 3}
      {:name "right-forearm", :size 3}
      {:name "abdomen", :size 6}
      {:name "left-kidney", :size 1}
      {:name "right-kidney", :size 1}
      {:name "left-hand", :size 2}
      {:name "right-hand", :size 2}
      {:name "left-knee", :size 2}
      {:name "right-knee", :size 2}
      {:name "left-thigh", :size 4}
      {:name "right-thigh", :size 4}
      {:name "left-lower-leg", :size 3}
      {:name "right-lower-leg", :size 3}
      {:name "left-achilles", :size 1}
      {:name "right-achilles", :size 1}
      {:name "left-foot", :size 2}
      {:name "right-foot", :size 2}]
```
Let’s break down this code!
## let
`let` binds names to values. You can think of `let` as short for *let it be*, which is also a beautiful Beatles song about programming. Here’s an example:
```clojure
(let [x 3]
  x)
; => 3

(def dalmatian-list
  ["Pongo" "Perdita" "Puppy 1" "Puppy 2"])

(let [dalmatians (take 2 dalmatian-list)]
  dalmatians)
; => ("Pongo" "Perdita")
```
`let` also introduces a new *scope*:
```clojure
(def x 0)
(let [x 1] x)
; => 1
```
You can reference existing bindings in your `let` binding:
```clojure
(def x 0)
(let [x (inc x)] x)
; => 1
```
You can also use rest parameters in `let`, just like you can in functions:
```clojure
(let [[pongo & dalmatians] dalmatian-list]
  [pongo dalmatians])
; => ["Pongo" ("Perdita" "Puppy 1" "Puppy 2")]
```
`let` forms have two main uses:
1. Provide clarity by allowing you to name things.
1. Allow you to evaluate an expression only once and reuse the result.

This is especially important when you need to reuse the result of an expensive function call, like a network API call. It’s also important when the expression has side effects.

Let’s have another look at the `let` form in our symmetrizing function so we can understand exactly what’s going on:
```clojure
(let [[part & remaining] remaining-asym-parts]
  (recur remaining
         (into final-body-parts
               (set [part (matching-part part)])))) 
```


As for the body of the `let` expression, you’ll learn about the meaning of `recur` in the next section. The function call
```clojure
(into final-body-parts
  (set [part (matching-part part)]))
```
Use the `set` function to create a set consisting of `part` and its matching part. Then use the function `into` to add the elements of that set to the vector `final-body-parts`. Here’s a simplified example:
```clojure
(into [] (set [:a :a]))
; => [:a]
```
First, `(set [:a :a])` returns the set `#{:a}`, because sets don’t contain duplicate elements. Then `(into [] #{:a})` returns the vector `[:a]`.

If we used the original expressions instead of using the names `part` and `remaining`, it would be a mess! Here’s an example:
```clojure
(recur (rest remaining-asym-parts)
       (into final-body-parts
             (set [(first remaining-asym-parts) (matching-part (first remaining-asym-parts))])))
```
So, `let` is a handy way to introduce local names for values, which helps simplify the code.

## loop
In our `symmetrize-body-parts` function we use `loop`, which provides another way to do recursion in Clojure. Let’s look at a simple example:
```clojure
(loop [iteration 0]
  (println (str "Iteration " iteration))
  (if (> iteration 3)
    (println "Goodbye!")
    (recur (inc iteration))))
; => Iteration 0
; => Iteration 1
; => Iteration 2
; => Iteration 3
; => Iteration 4
; => Goodbye!
```
You could in fact accomplish the same thing by just using a normal function definition:
```clojure
(defn recursive-printer
  ([]
     (recursive-printer 0))
  ([iteration]
     (println (str "Iteration " iteration))
     (if (> iteration 3)
       (println "Goodbye!")
       (recursive-printer (inc iteration)))))
(recursive-printer)
; => Iteration 0
; => Iteration 1
; => Iteration 2
; => Iteration 3
; => Iteration 4
; => Goodbye!
```
But as you can see, this is a bit more verbose. Also, `loop` has much better performance. In our symmetrizing function, we’ll use `loop` to go through each element in the asymmetrical list of body parts.

## Regular Expressions
*Regular expressions* are tools for performing pattern matching on text. The literal notation for a regular expression is to place the expression in quotes after a hash mark:
```clojure
#"regular-expression"
```
`clojure.string/replace` uses the regular expression `#"^left-"` to match strings starting with `"left-"` in order to replace `"left-"` with `"right-"`.

You can test this with `re-find`, which checks whether a string matches the pattern described by a regular expression, returning the matched text or `nil` if there is no match:
```clojure
(re-find #"^left-" "left-eye")
; => "left-"

(re-find #"^left-" "cleft-chin")
; => nil

(re-find #"^left-" "wongleblart")
; => nil
```
Here are a couple of examples of `matching-part` using a regex to replace `"left-"` with `"right-"`:
```clojure
(defn matching-part
  [part]
  {:name (clojure.string/replace (:name part) #"^left-" "right-")
   :size (:size part)})
(matching-part {:name "left-eye" :size 1})
; => {:name "right-eye" :size 1}]

(matching-part {:name "head" :size 3})
; => {:name "head" :size 3}]
```
Notice that the name `"head"` is returned as is.

## Symmetrizer

Now let’s go back to the full symmetrizer and analyze it in more detail:

```clojure
(def asym-hobbit-body-parts [{:name "head" :size 3}
                             {:name "left-eye" :size 1}
                             {:name "left-ear" :size 1}
                             {:name "mouth" :size 1}
                             {:name "nose" :size 1}
                             {:name "neck" :size 2}
                             {:name "left-shoulder" :size 3}
                             {:name "left-upper-arm" :size 3}
                             {:name "chest" :size 10}
                             {:name "back" :size 10}
                             {:name "left-forearm" :size 3}
                             {:name "abdomen" :size 6}
                             {:name "left-kidney" :size 1}
                             {:name "left-hand" :size 2}
                             {:name "left-knee" :size 2}
                             {:name "left-thigh" :size 4}
                             {:name "left-lower-leg" :size 3}
                             {:name "left-achilles" :size 1}
                             {:name "left-foot" :size 2}])


(defn matching-part
  [part]
  {:name (clojure.string/replace (:name part) #"^left-" "right-")
   :size (:size part)})

(defn symmetrize-body-parts
  "Expects a seq of maps that have a :name and :size"
  [asym-body-parts]
   (loop [remaining-asym-parts asym-body-parts 
         final-body-parts []]
     (if (empty? remaining-asym-parts) 
      final-body-parts
       (let [[part & remaining] remaining-asym-parts] 
         (recur remaining 
               (into final-body-parts
                     (set [part (matching-part part)])))))))
```
## Better Symmetrizer with reduce
The pattern of *process each element in a sequence and build a result* is so common that there’s a built-in function for it called `reduce`. Here’s a simple example:
```clojure
;; sum with reduce
(reduce + [1 2 3 4])
; => 10
```
This is like telling Clojure to do this:
```clojure
(+ (+ (+ 1 2) 3) 4)
```

`reduce` also takes an optional initial value. The initial value here is `15`:
```clojure
(reduce + 15 [1 2 3 4])
```
To further understand how `reduce` works, here’s one way that you could implement it:
```clojure
(defn my-reduce
  ([f initial coll]
   (loop [result initial
          remaining coll]
     (if (empty? remaining)
       result
       (recur (f result (first remaining)) (rest remaining)))))
  ([f [head & tail]]
   (my-reduce f head tail)))
```
We could reimplement our symmetrizer as follows:
```clojure
(defn better-symmetrize-body-parts
  "Expects a seq of maps that have a :name and :size"
  [asym-body-parts]
  (reduce (fn [final-body-parts part]
            (into final-body-parts (set [part (matching-part part)])))
          []
          asym-body-parts))
```
Using `reduce` is also more expressive. If readers of your `code` encounter loop, they won’t be sure exactly what the loop is doing without reading all of the code. But if they see `reduce`, they’ll immediately know that the purpose of the code is to process the elements of a collection to build a result.

## Hobbit Violence
```clojure
(defn hit
  [asym-body-parts]
  (let [sym-parts (➊better-symmetrize-body-parts asym-body-parts)
        ➋body-part-size-sum (reduce + (map :size sym-parts))
        target (rand body-part-size-sum)]
    ➌(loop [[part & remaining] sym-parts
           accumulated-size (:size part)]
      (if (> accumulated-size target)
        part
        (recur remaining (+ accumulated-size (:size (first remaining))))))))
```
`hit` works by taking a vector of asymmetrical body parts, symmetrizing it at ➊, and then summing the sizes of the parts at ➋. Once we sum the sizes, it’s like each number from 1 through `body-part-size-sum` corresponds to a body part; 1 might correspond to the left eye, and 2, 3, 4 might correspond to the head. This makes it so when you hit a body part (by choosing a random number in this range), the likelihood that a particular body part is hit will depend on the size of the body part. 

*Figure 3-1: Body parts correspond to ranges of numbers and get hit if the target falls within that range:*
<div align="center">
  <img src="https://www.braveclojure.com/assets/images/cftbat/do-things/hobbit-hit-line.png">
</div>

Finally, one of these numbers is randomly chosen, and then we use `loop` at ➌ to find and return the body part that corresponds to the number. The loop does this by keeping track of the accumulated sizes of parts that we’ve checked and checking whether the accumulated size is greater than the target. I visualize this process as lining up the body parts with a row of numbered slots. After I line up a body part, I ask myself, “Have I reached the target yet?” If I have, that means the body part I just lined up was the one hit. Otherwise, I just keep lining up those parts.

For example, say that your list of parts is head, left eye, and left hand, like in Figure 3-1. After taking the first part, the head, the accumulated size is 3. The body part is hit when the accumulated sizes exceeds the target, so if the target is less than 3, then the head was hit. Otherwise, you take the next part, the left eye, and increase the accumulated size to 4, yielding a hit if the target is greater than or equal to 3 and less than 4. Similarly, the left hand gets hit if the target is greater than or equal to 4 and less than 6.

Here are some sample runs of the `hit` function:
```clojure
(hit asym-hobbit-body-parts)
; => {:name "right-upper-arm", :size 3}

(hit asym-hobbit-body-parts)
; => {:name "chest", :size 10}

(hit asym-hobbit-body-parts)
; => {:name "left-eye", :size 1}
```
# Summary
# Exercises
