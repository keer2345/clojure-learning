**Chapter 04 [Core Functions in Depth](https://www.braveclojure.com/core-functions-in-depth/)**

In this chapter, you’ll learn about Clojure’s underlying concept of *programming to abstractions* and about the sequence and collection abstractions.

You’ll also learn about *lazy sequences*. This will give you the grounding you need to read the documentation for functions you haven’t used before and to understand what’s happening when you give them a try.

You’ll learn how to work with lists, vectors, maps, and sets with the functions `map`, `reduce`, `into`, `conj`, `concat`, `some`, `filter`, `take`, `drop`, `sort`, `sort-by`, and `identity`. You’ll also learn how to create new functions with `apply`, `partial`, and `complement`. All this information will help you understand how to do things the Clojure way, and it will give you a solid foundation for writing your own code as well as for reading and learning from others’ projects.

Finally, you’ll learn how to parse and query a CSV of vampire data to determine what nosferatu lurk in your hometown.


<!-- vim-markdown-toc GFM -->

* [Programming to Abstractions](#programming-to-abstractions)
    * [Treating Lists, Vectors, Sets, and Maps as Sequences](#treating-lists-vectors-sets-and-maps-as-sequences)
    * [first, rest, and cons](#first-rest-and-cons)
    * [Abstraction Through Indirection](#abstraction-through-indirection)
* [Seq Function Examples](#seq-function-examples)
    * [map](#map)
    * [reduce](#reduce)
    * [take, drop, take-while, and drop-while](#take-drop-take-while-and-drop-while)
    * [filter and some](#filter-and-some)
    * [sort and sort-by](#sort-and-sort-by)
    * [concat](#concat)
* [Lazy Seqs](#lazy-seqs)
    * [Demonstrating Lazy Seq Efficiency](#demonstrating-lazy-seq-efficiency)
    * [Infinite Sequences](#infinite-sequences)
* [The Collection Abstraction](#the-collection-abstraction)
    * [into](#into)
    * [conj](#conj)
* [Function Functions](#function-functions)
    * [apply](#apply)
    * [partial](#partial)
    * [complement](#complement)
* [A Vampire Data Analysis Program for the FWPD](#a-vampire-data-analysis-program-for-the-fwpd)
* [Summary](#summary)
* [Exercises](#exercises)

<!-- vim-markdown-toc -->

# Programming to Abstractions
`map` doesn’t care about how lists, vectors, sets, and maps are implemented. It only cares about whether it can perform sequence operations on them. Let’s look at how `map` is defined in terms of the sequence abstraction so you can understand programming to abstractions in general.

## Treating Lists, Vectors, Sets, and Maps as Sequences

$$y_1=f(x_1), y_2=f(x_2), ... y_n=f(x_n)$$

<div align="center">
    <img src="https://www.braveclojure.com/assets/images/cftbat/core-functions-in-depth/mapping.png">
</div>

```clojure
(defn titleize
  [topic]
  (str topic " for the Brave and True"))

(map titleize ["Hamsters" "Ragnarok"])
; => ("Hamsters for the Brave and True" "Ragnarok for the Brave and True")

(map titleize '("Empathy" "Decorating"))
; => ("Empathy for the Brave and True" "Decorating for the Brave and True")

(map titleize #{"Elbows" "Soap Carving"})
; => ("Elbows for the Brave and True" "Soap Carving for the Brave and True")

(map #(titleize (second %)) {:uncomfortable-thing "Winking"})
; => ("Winking for the Brave and True")
```

## first, rest, and cons
## Abstraction Through Indirection
When it comes to sequences, Clojure also creates indirection by doing a kind of lightweight type conversion, producing a data structure that works with an abstraction’s functions.
```clojure
(seq '(1 2 3))
; => (1 2 3)

(seq [1 2 3])
; => (1 2 3)

(seq #{1 2 3})
; => (1 2 3)

(seq {:name "Bill Compton" :occupation "Dead mopey guy"})
; => ([:name "Bill Compton"] [:occupation "Dead mopey guy"])
```

You can convert the seq back `into` a map by using into to stick the result into an empty map (you’ll look at `into` closely later):
```clojure
(into {} (seq {:a 1 :b 2 :c 3}))
; => {:a 1, :c 3, :b 2}
```
So, Clojure’s sequence functions use `seq` on their arguments. The sequence functions are defined in terms of the sequence abstraction, using `first`, `rest`, and `cons`. As long as a data structure implements the sequence abstraction, it can use the extensive seq library, which includes such superstar functions as `reduce`, `filter`, `distinct`, `group-by`, and dozens more.

# Seq Function Examples
## map
```clojure
(map inc [1 2 3])
; => (2 3 4)
```

```clojure
(map str ["a" "b" "c"] ["A" "B" "C"])
; => ("aA" "bB" "cC")
```
It’s as if `map` does the following:
```clojure
(list (str "a" "A") (str "b" "B") (str "c" "C"))
```



The following example shows how you could use this capability if you were a vampire trying to curb your human consumption. You have two vectors, one representing human intake in liters and another representing critter intake for the past four days. The `unify-diet-data` function takes a single day’s data for both human and critter feeding and unifies the two into a single map:
```clojure
(def human-consumption   [8.1 7.3 6.6 5.0])
(def critter-consumption [0.0 0.2 0.3 1.1])
(defn unify-diet-data
  [human critter]
  {:human human
   :critter critter})

(map unify-diet-data human-consumption critter-consumption)
; => ({:human 8.1, :critter 0.0}
      {:human 7.3, :critter 0.2}
      {:human 6.6, :critter 0.3}
      {:human 5.0, :critter 1.1})
```

Another fun thing you can do with `map` is pass it a collection of functions. You could use this if you wanted to perform a set of calculations on different collections of numbers, like so:
```clojure
(def sum #(reduce + %))
(def avg #(/ (sum %) (count %)))
(defn stats
  [numbers]
  (map #(% numbers) [sum count avg]))

(stats [3 4 10])
; => (17 3 17/3)

(stats [80 1 44 13 6])
; => (144 5 144/5)
```

```clojure
(def identities
  [{:alias "Batman" :real "Bruce Wayne"}
   {:alias "Spider-Man" :real "Peter Parker"}
   {:alias "Santa" :real "Your mom"}
   {:alias "Easter Bunny" :real "Your dad"}])

(map :real identities)
; => ("Bruce Wayne" "Peter Parker" "Your mom" "Your dad")
```

## reduce
The first use is to transform a map’s values, producing a new map with the same keys but with updated values:

```clojure
(reduce (fn [new-map [key val]]
          (assoc new-map key (inc val)))
        {}
        {:max 30 :min 10})
; => {:max 31, :min 11}
```
It’s as if `reduce` does this:
```clojure
(assoc (assoc {} :max (inc 30))
       :min (inc 10))
```

```clojure
(reduce (fn [new-map [key val]]
          (if (> val 4)
            (assoc new-map key val)
            new-map))
        {}
        {:human 4.1
         :critter 3.9})
; => {:human 4.1}
```
## take, drop, take-while, and drop-while
```clojure
(take 3 [1 2 3 4 5 6 7 8 9 10])
; => (1 2 3)

(drop 3 [1 2 3 4 5 6 7 8 9 10])
; => (4 5 6 7 8 9 10)
```
```clojure
(def food-journal
  [{:month 1 :day 1 :human 5.3 :critter 2.3}
   {:month 1 :day 2 :human 5.1 :critter 2.0}
   {:month 2 :day 1 :human 4.9 :critter 2.1}
   {:month 2 :day 2 :human 5.0 :critter 2.5}
   {:month 3 :day 1 :human 4.2 :critter 3.3}
   {:month 3 :day 2 :human 4.0 :critter 3.8}
   {:month 4 :day 1 :human 3.7 :critter 3.9}
   {:month 4 :day 2 :human 3.7 :critter 3.6}])

(take-while #(< (:month %) 3) food-journal)
; => ({:month 1 :day 1 :human 5.3 :critter 2.3}
      {:month 1 :day 2 :human 5.1 :critter 2.0}
      {:month 2 :day 1 :human 4.9 :critter 2.1}
      {:month 2 :day 2 :human 5.0 :critter 2.5})

(drop-while #(< (:month %) 3) food-journal)
; => ({:month 3 :day 1 :human 4.2 :critter 3.3}
      {:month 3 :day 2 :human 4.0 :critter 3.8}
      {:month 4 :day 1 :human 3.7 :critter 3.9}
      {:month 4 :day 2 :human 3.7 :critter 3.6})

(take-while #(< (:month %) 4)
            (drop-while #(< (:month %) 2) food-journal))
; => ({:month 2 :day 1 :human 4.9 :critter 2.1}
      {:month 2 :day 2 :human 5.0 :critter 2.5}
      {:month 3 :day 1 :human 4.2 :critter 3.3}
      {:month 3 :day 2 :human 4.0 :critter 3.8})
```
## filter and some
```clojure
(filter #(< (:human %) 5) food-journal)
; => ({:month 2 :day 1 :human 4.9 :critter 2.1}
      {:month 3 :day 1 :human 4.2 :critter 3.3}
      {:month 3 :day 2 :human 4.0 :critter 3.8}
      {:month 4 :day 1 :human 3.7 :critter 3.9}
      {:month 4 :day 2 :human 3.7 :critter 3.6})

(filter #(< (:month %) 3) food-journal)
; => ({:month 1 :day 1 :human 5.3 :critter 2.3}
      {:month 1 :day 2 :human 5.1 :critter 2.0}
      {:month 2 :day 1 :human 4.9 :critter 2.1}
      {:month 2 :day 2 :human 5.0 :critter 2.5})
```
This use is perfectly fine, but `filter` can end up processing all of your data, which isn’t always necessary. Because the food journal is already sorted by date, we know that `take-while` will return the data we want without having to examine any of the data we won’t need. Therefore, `take-while` can be more efficient.


Often, you want to know whether a collection contains any values that test true for a predicate function. The `some` function does that, returning the first truthy value (any value that’s not `false` or `nil`) returned by a predicate function:
```clojure
(some #(> (:critter %) 5) food-journal)
; => nil

(some #(> (:critter %) 3) food-journal)
; => true
```
```clojure
(some #(and (> (:critter %) 3) %) food-journal)
; => {:month 3 :day 1 :human 4.2 :critter 3.3}
```

Here, a slightly different anonymous function uses `and` to first check whether the condition `(> (:critter %) 3)` is true, and then returns the entry when the condition is indeed true.

## sort and sort-by
```clojure
(sort [3 1 2])
; => (1 2 3)

(sort-by count ["aaa" "c" "bb"])
; => ("c" "bb" "aaa")
```
## concat

```clojure
(concat [1 2] [3 4])
; => (1 2 3 4)
```

# Lazy Seqs
Deferring the computation until the moment it’s needed makes your programs more efficient, and it has the surprising benefit of allowing you to construct infinite sequences.
## Demonstrating Lazy Seq Efficiency
Here is a subset of a vampire database:
```clojure
(def vampire-database
  {0 {:makes-blood-puns? false, :has-pulse? true  :name "McFishwich"}
   1 {:makes-blood-puns? false, :has-pulse? true  :name "McMackson"}
   2 {:makes-blood-puns? true,  :has-pulse? false :name "Damon Salvatore"}
   3 {:makes-blood-puns? true,  :has-pulse? true  :name "Mickey Mouse"}})

(defn vampire-related-details
  [social-security-number]
  (Thread/sleep 1000)
  (get vampire-database social-security-number))

(defn vampire?
  [record]
  (and (:makes-blood-puns? record)
       (not (:has-pulse? record))
       record))

(defn identify-vampire
  [social-security-numbers]
  (first (filter vampire?
                 (map vampire-related-details social-security-numbers))))
```
To show how much time it takes to run these functions, you can use the `time` operation.
```clojure
(time (vampire-related-details 0))
; => "Elapsed time: 1001.042 msecs"
; => {:name "McFishwich", :makes-blood-puns? false, :has-pulse? true}
```

```clojure
(time (def mapped-details (map vampire-related-details (range 0 1000000))))
; => "Elapsed time: 0.049 msecs"
; => #'user/mapped-details
```
In the previous example, `mapped-details` is unrealized. Once you try to access a member of `mapped-details`, it will use its recipe to generate the element you’ve requested, and you’ll incur the one-second-per-database-lookup cost:
```clojure
(time (first mapped-details))
; => "Elapsed time: 32030.767 msecs"
; => {:name "McFishwich", :makes-blood-puns? false, :has-pulse? true}
```

This operation took about 32 seconds. That’s much better than one million seconds, but it’s still 31 seconds more than we would have expected. After all, you’re only trying to access the very first element, so it should have taken only one second.

Thankfully, lazy seq elements need to be realized only once. Accessing the first element of `mapped-details` again takes almost no time:
```clojure
(time (first mapped-details))
; => "Elapsed time: 0.022 msecs"
; => {:name "McFishwich", :makes-blood-puns? false, :has-pulse? true}
```
With all this newfound knowledge, you can efficiently mine the vampire database to find the fanged culprit:
```clojure
(time (identify-vampire (range 0 1000000)))
"Elapsed time: 32019.912 msecs"
; => {:name "Damon Salvatore", :makes-blood-puns? true, :has-pulse? false}
```
Ooh! That’s why Damon makes those creepy puns!

## Infinite Sequences
Clojure comes with a few functions to create infinite sequences. One easy way to create an infinite sequence is with `repeat`, which creates a sequence whose every member is the argument you pass:
```clojure
(concat (take 8 (repeat "na")) ["Batman!"])
; => ("na" "na" "na" "na" "na" "na" "na" "na" "Batman!")
```
You can also use `repeatedly`, which will call the provided function to generate each element in the sequence:
```clojure
(take 3 (repeatedly (fn [] (rand-int 10))))
; => (1 4 0)
```
A lazy seq’s recipe doesn’t have to specify an endpoint.
```clojure
(defn even-numbers
  ([] (even-numbers 0))
  ([n] (cons n (lazy-seq (even-numbers (+ n 2))))))

(take 10 (even-numbers))
; => (0 2 4 6 8 10 12 14 16 18)
```
# The Collection Abstraction
The collection abstraction is closely related to the sequence abstraction. All of Clojure’s core data structures—vectors, maps, lists, and sets—take part in both abstractions.
```clojure
(empty? [])
; => true

(empty? ["no!"])
; => false
```
## into
```clojure
(map identity {:sunlight-reaction "Glitter!"})
; => ([:sunlight-reaction "Glitter!"])

(into {} (map identity {:sunlight-reaction "Glitter!"}))
; => {:sunlight-reaction "Glitter!"}
```
This will work with other data structures as well:
```clojure
(map identity [:garlic :sesame-oil :fried-eggs])
; => (:garlic :sesame-oil :fried-eggs)

(into [] (map identity [:garlic :sesame-oil :fried-eggs]))
; => [:garlic :sesame-oil :fried-eggs]
```

```clojure
(map identity [:garlic-clove :garlic-clove])
; => (:garlic-clove :garlic-clove)

(into #{} (map identity [:garlic-clove :garlic-clove]))
; => #{:garlic-clove}
```
The first argument of `into` doesn’t have to be empty.
```clojure
(into {:favorite-emotion "gloomy"} [[:sunlight-reaction "Glitter!"]])
; => {:favorite-emotion "gloomy" :sunlight-reaction "Glitter!"}

(into ["cherry"] '("pine" "spruce"))
; => ["cherry" "pine" "spruce"]
```

And, of course, both arguments can be the same type. In this next example, both arguments are maps, whereas all the previous examples had arguments of different types. It works as you’d expect, returning a new map with the elements of the second map added to the first:

```clojure
(into {:favorite-animal "kitty"} {:least-favorite-smell "dog"
                                  :relationship-with-teenager "creepy"})
; => {:favorite-animal "kitty"
      :relationship-with-teenager "creepy"
      :least-favorite-smell "dog"}
```

## conj
`conj` also adds elements to a collection, but it does it in a slightly different way:
```clojure
(into [0] [1])
; => [0 1]

(conj [0] [1])
; => [0 [1]]

(conj [0] 1)
; => [0 1]
```
You can supply as many elements to add with `conj` as you want, and you can also add to other collections like maps:
```clojure
(conj [0] 1 2 3 4)
; => [0 1 2 3 4]

(conj {:time "midnight"} [:place "ye olde cemetarium"])
; => {:place "ye olde cemetarium" :time "midnight"}
```

`conj` and `into` are so similar that you could even define `conj` in terms of `into`:
```clojure
(defn my-conj
  [target & additions]
  (into target additions))

(my-conj [0] 1 2 3)
; => [0 1 2 3]
```

# Function Functions
Two of Clojure’s functions, might seem especially weird because they both accept and return functions:
- `apply`
- `partial`

## apply

`apply` explodes a seqable data structure so it can be passed to a function that expects a rest parameter. For example, `max` takes any number of arguments and returns the greatest of all the arguments. Here’s how you’d find the greatest number:
```clojure
(max 0 1 2)
; => 2
```

But what if you want to find the greatest element of a vector? You can’t just pass the vector to `max`:

```clojure
(max [0 1 2])
; => [0 1 2]
```
In this case you’re only passing it a vector containing all the numbers you want to compare, rather than passing in the numbers as separate arguments. `apply` is perfect for this situation:
```clojure
(apply max [0 1 2])
; => 2
```
```clojure
(defn my-into
  [target additions]
  (apply conj target additions))

(my-into [0] [1 2 3])
; => [0 1 2 3]
```
## partial
`partial` takes a function and any number of arguments. It then returns a new function. When you call the returned function, it calls the original function with the original arguments you supplied it along with the new arguments.
```clojure
(def add10 (partial + 10))
(add10 3) 
; => 13
(add10 5) 
; => 15

(def add-missing-elements
  (partial conj ["water" "earth" "air"]))

(add-missing-elements "unobtainium" "adamantium")
; => ["water" "earth" "air" "unobtainium" "adamantium"]
```

To help clarify how `partial` works, here’s how you might define it:
```clojure
(defn my-partial
  [partialized-fn & args]
  (fn [& more-args]
    (apply partialized-fn (into args more-args))))

(def add20 (my-partial + 20))
(add20 3) 
; => 23
```

The anonymous function is defined like this:
```clojure
(fn [& more-args]
  (apply + (into [20] more-args)))
```

In general, you want to use partials when you find you’re repeating the same combination of function and arguments in many different contexts. This toy example shows how you could use `partial` to specialize a logger, creating a `warn` function:
```clojure
(defn lousy-logger
  [log-level message]
  (condp = log-level
    :warn (clojure.string/lower-case message)
    :emergency (clojure.string/upper-case message)))

(def warn (partial lousy-logger :warn))

(warn "Red light ahead")
; => "red light ahead"
```
## complement
Earlier you created the identify-vampire function to find one vampire amid a million people. What if you wanted to create a function to find all humans? Perhaps you want to send them thank-you cards for not being an undead predator. Here’s how you could do it:
```clojure
(defn identify-humans
  [social-security-numbers]
  (filter #(not (vampire? %))
          (map vampire-related-details social-security-numbers)))
```

```clojure
(def not-vampire? (complement vampire?))
(defn identify-humans
  [social-security-numbers]
  (filter not-vampire?
          (map vampire-related-details social-security-numbers)))
```

Here’s how you might implement `complement`:

```clojure
(defn my-complement
  [fun]
  (fn [& args]
    (not (apply fun args))))

(def my-pos? (complement neg?))
(my-pos? 1)  
; => true

(my-pos? -1) 
; => false
```
As you can see, `complement` is a humble function. It does one little thing and does it well. `complement` made it trivial to create a `not-vampire?` function, and anyone reading the code could understand the code’s intention.

This won’t MapReduce terabytes of data for you or anything like that, but it does demonstrate the power of higher-order functions. They allow you to build up libraries of utility functions in a way that is impossible in some languages. In aggregate, these utility functions make your life a lot easier.

# A Vampire Data Analysis Program for the FWPD
To pull everything together, let’s write the beginnings of a sophisticated vampire data analysis program for the Forks, Washington Police Department (FWPD).

The FWPD has a fancy new database technology called CSV (*comma-separated values*). Your job is to parse this state-of-the-art CSV and analyze it for potential vampires. We’ll do that by filtering on each suspect’s glitter index, a 0–10 prediction of the suspect’s vampireness developed by some teenage girl. Go ahead and create a new Leiningen project for your tool:

```clojure
lein new app fwpd
```

Under the new fwpd directory, create a file named *suspects.csv* and enter contents like the following:

```clojure
Edward Cullen,10
Bella Swan,0
Charlie Swan,0
Jacob Black,3
Carlisle Cullen,6
```

Now it’s time to get your hands dirty by building up the *fwpd/src/fwpd/core.clj* file. I recommend that you start a new REPL session so you can try things out as you go along. In Emacs you can do this by opening *fwpd/src/fwpd/core.clj* and running **M-x** `cider-restart`. Once the REPL is started, delete the contents of *core.clj*, and then add this:

```clojure
(ns fwpd.core)
(def filename "suspects.csv")
```

The first line establishes the namespace, and the second just makes it a tiny bit easier to refer to the CSV you created. You can do a quick sanity check in your REPL by compiling your file (**C-c C-k** in Emacs) and running this:

```clojure
(slurp filename)
; => "Edward Cullen,10\nBella Swan,0\nCharlie Swan,0\nJacob Black,3\nCarlisle Cullen,6"
```

If the `slurp` function doesn’t return the preceding string, try restarting your REPL session with *core.clj* open.

Next, add this to *core.clj*:
```clojure
(def vamp-keys [:name :glitter-index])

(defn str->int
  [str]
  (Integer. str))

(def conversions {:name identity
                  :glitter-index str->int})

(defn convert
  [vamp-key value]
  ((get conversions vamp-key) value))
```
```clojure
(convert :glitter-index "3")
; => 3
```

Now add this to your file:

```clojure
(defn parse
  "Convert a CSV into rows of columns"
  [string]
  (map #(clojure.string/split % #",")
       (clojure.string/split string #"\n")))
```


The `parse` function takes a string and first splits it on the newline character to create a seq of strings. Next, it maps over the seq of strings, splitting each one on the comma character. Try running `parse` on your CSV:

```clojure
(parse (slurp filename))
; => (["Edward Cullen" "10"] ["Bella Swan" "0"] ["Charlie Swan" "0"]
      ["Jacob Black" "3"] ["Carlisle Cullen" "6"])
```

The next bit of code takes the seq of vectors and combines it with your vamp keys to create maps:

```clojure
(defn mapify
  "Return a seq of maps like {:name \"Edward Cullen\" :glitter-index 10}"
  [rows]
  (map (fn [unmapped-row]
         (reduce (fn [row-map [vamp-key value]]
                   (assoc row-map vamp-key (convert vamp-key value)))
                 {}
                 (map vector vamp-keys unmapped-row)))
       rows))
```

```clojure
(first (mapify (parse (slurp filename))))
; => {:glitter-index 10, :name "Edward Cullen"}
```

Finally, add this `glitter-filter` function:
```clojure
(defn glitter-filter
  [minimum-glitter records]
  (filter #(>= (:glitter-index %) minimum-glitter) records))
```

This takes fully mapified vampire records and filters out those with a `:glitter-index` less than the provided `minimum-glitter`:
```clojure
(glitter-filter 3 (mapify (parse (slurp filename))))
({:name "Edward Cullen", :glitter-index 10}
 {:name "Jacob Black", :glitter-index 3}
 {:name "Carlisle Cullen", :glitter-index 6})
```

Et voilà! You are now one step closer to fulfilling your dream of being a supernatural-creature-hunting vigilante. You better go round up those sketchy characters!

# Summary
# Exercises
