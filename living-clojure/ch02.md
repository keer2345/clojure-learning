**Chapter 02 Flow and Functional Transformations**

# Controlling the Flow with Logic

``` clojure
(class true)
;; -> java.lang.Boolean

(true? true)
;; -> true
(true? false)
;; -> false

(false? false)
;; -> true
(false? true)
;; -> false

(nil? nil)
;; -> true
(nil? 1)
;; -> false

(not true)
;; -> false
(not false)
;; -> true

(not nil)
;; -> true

(not "hi")
;; -> false
```

Letâ€™s check equality of the two labels of these bottles:

``` clojure
(= :drinkme :drinkme)
;; -> true

(= :drinkme 4)
;; -> false

(= '(:drinkme :bottle) [:drinkme :bottle])
;; -> true

(not= :drinkme :4)
;; -> true
```
## Logic Tests You Can Use on Collections

``` clojure
(empty? [:table :door :key])
;; -> false
(empty? [])
;; -> true
(empty? {})
;; -> true
(empty? '())
;; -> true
```

``` clojure
(defn empty?
	[coll] (not (seq coll)))
```

``` clojure
(seq [1 2 3])
;; -> (1 2 3)
(class [1 2 3])
;; -> clojure.lang.PersistentVector
(class (seq [1 2 3]))
;; -> clojure.lang.PersistentVector$ChunkedSeq
(seq [])
;; -> nil
```

``` clojure
(empty? [])
;; -> true
;; use this to check for not empty
(seq [])
;; -> nil

(every? odd? [1 3 5])
;; -> true
(every? odd? [1 2 3 4 5])
;; -> false
```

``` clojure
(defn drinkable? [x]
(= x :drinkme))
;; -> #'user/drinkable?
(every? drinkable? [:drinkme :drinkme])
;; -> true
(every? drinkable? [:drinkme :poison])
;; -> false

(every? (fn [x] (= x :drinkme)) [:drinkme :drinkme])
;; -> true

(every? #(= % :drinkme) [:drinkme :drinkme])
;; -> true

(not-any? #(= % :drinkme) [:drinkme :poison])
;; -> false
(not-any? #(= % :drinkme) [:poison :poison])
;; -> true

(some #(> % 3) [1 2 3 4 5])
;; -> true
```

``` clojure
(#{1 2 3 4 5} 3)
;; -> 3

(some #{3} [1 2 3 4 5])
;; -> 3

(some #{4 5} [1 2 3 4 5])
;; -> 4

(some #{nil} [nil nil nil])
;; -> nil
(some #{false} [false false false])
;; -> nil
```

## Harnessing the Power of Flow Control

``` clojure
(if true "it is true" "it is false")
;; -> "it is true"

(if false "it is true" "it is false")
;; -> "it is false"

(if nil "it is true" "it is false")
;; -> "it is false"

(if (= :drinkme :drinkme)
	"Try it"
	"Don't try it")
;; -> "Try it"

(let [need-to-grow-small (> 5 3)]
	(if need-to-grow-small
		"drink bottle"
		"don't drink bottle"))
;; -> "drink bottle"
```

``` clojure
(defn drink [need-to-grow-small]
	(when need-to-grow-small "drink bottle"))

(drink true)
;; -> "drink bottle"

(drink false)
;; -> nil
```

``` clojure
(let [bottle "drinkme"]
	(cond
		(= bottle "poison") "don't touch"
		(= bottle "drinkme") "grow smaller"
		(= bottle "empty") "all gone"))
;; -> "grow smaller"

(let [x 5]
	(cond
		(> x 10) "bigger than 10"
		(> x 4) "bigger than 4"
		(> x 3) "bigger than 3"))
;; -> "bigger than 4"

(let [x 1]
	(cond
		(> x 10) "bigger than 10"
		(> x 4) "bigger than 4"
		(> x 3) "bigger than 3"))
;; -> nil

(let [bottle "mystery"]
	(cond
		(= bottle "poison") "don't touch"
		(= bottle "drinkme") "grow smaller"
		(= bottle "empty") "all gone"
:else "unknown"))
;; -> "unknown"

(let [bottle "mystery"]
	(cond
		(= bottle "poison") "don't touch"
		(= bottle "drinkme") "grow smaller"
		(= bottle "empty") "all gone"
		"default" "unknown"))
;; -> "unknown"

(let [bottle "drinkme"]
	(case bottle
		"poison" "don't touch"
		"drinkme" "grow smaller"
		"empty" "all gone"))
;; -> "grow smaller"

(let [bottle "mystery"]
	(case bottle
		"poison" "don't touch"
		"drinkme" "grow smaller"
		"empty" "all gone"))
;; -> IllegalArgumentException No matching clause: mystery

(let [bottle "mystery"]
	(case bottle
		"poison" "don't touch"
		"drinkme" "grow smaller"
		"empty" "all gone"
		"unknown"))
;; -> "unknown"
```

# Functions Creating Functions and Other Neat Expressions

``` clojure
(defn grow [name direction]
	(if (= direction :small)
		(str name " is growing smaller")
		(str name " is growing bigger")))

;; -> #'user/grow
(grow "Alice" :small)
;; -> "Alice is growing smaller"
(grow "Alice" :big)
;; -> "Alice is growing bigger"

(partial grow "Alice")
;; -> #<core$partial$fn__4228 clojure.core$partial$fn__4228@1759817d>
((partial grow "Alice") :small)
;; -> "Alice is growing smaller"
```

``` clojure
(defn toggle-grow [direction]
	(if (= direction :small) :big :small))
;; -> #'user/toggle-grow
(toggle-grow :big)
;; -> :small
(toggle-grow :small)
;; -> :big

(defn oh-my [direction]
	(str "Oh My! You are growing " direction))
;; -> #'user/oh-my

(oh-my (toggle-grow :small))
;; -> "Oh My! You are growing :big"

(defn surprise [direction]
	((comp oh-my toggle-grow) direction))

(surprise :small)
;; -> "Oh My! You are growing :big"
```

``` clojure
(defn adder [x y]
(+ x y))
;; -> #'user/adder
(adder 3 4)
;; -> 7
(def adder-5 (partial adder 5))
;; -> #'user/adder-5
(adder-5 10)
;; -> 15
```
# Destructuring

``` clojure
(let [[color size] ["blue" "small"]]
	(str "The " color " door is " size))
;; -> "The blue door is small"

(let [x ["blue" "small"]
		color (first x)
		size (last x)]
	(str "The " color " door is " size))
;; -> "The blue door is small"

(let [[color [size] :as original] ["blue" ["small"]]]
	{:color color :size size :original original})
;; -> {:color "blue", :size "small", :original ["blue" ["small"]]}
``` 
``` clojure
(let [{flower1 :flower1 flower2 :flower2}
		{:flower1 "red" :flower2 "blue"}]
	(str "The flowers are " flower1 " and " flower2))
;; -> "The flowers are red and blue"

(let [{flower1 :flower1 flower2 :flower2 :or {flower2 "missing"}}
		{:flower1 "red"}]
	(str "The flowers are " flower1 " and " flower2))
;; -> "The flowers are red and missing"

(let [{flower1 :flower1 :as all-flowers}
		{:flower1 "red"}]
	[flower1 all-flowers])
;; -> ["red" {:flower1 "red"}]

(let [{:keys [flower1 flower2]}
		{:flower1 "red" :flower2 "blue"}]
	(str "The flowers are " flower1 " and " flower2))
;; -> "The flowers are red and blue"

(defn flower-colors [colors]
	(str "The flowers are "
		(:flower1 colors)
		" and "
		(:flower2 colors)))
(flower-colors {:flower1 "red" :flower2 "blue"})
;; -> "The flowers are red and blue"

(defn flower-colors [{:keys [flower1 flower2]}]
	(str "The flowers are " flower1 " and " flower2))
(flower-colors {:flower1 "red" :flower2 "blue"})
;; -> "The flowers are red and blue"
```

# The Power of Laziness

``` clojure
(take 5 (range))
;; -> (0 1 2 3 4)
(take 10 (range))
;; -> (0 1 2 3 4 5 6 7 8 9)
```

``` clojure
(range 5)
;; -> (0 1 2 3 4)
(class (range 5))
;; -> clojure.lang.LazySeq
```

``` clojure
(take 10 (range))
;; -> (0 1 2 3 4 5 6 7 8 9)

(count (take 1000 (range)))
;; -> 1000

(count (take 
;; -> 100000
```

``` clojure
(repeat 3 "rabbit")
;; -> ("rabbit" "rabbit" "rabbit")
(class (repeat 3 "rabbit"))
;; -> clojure.lang.LazySeq

(take 5 (repeat "rabbit"))
;; -> ("rabbit" "rabbit" "rabbit" "rabbit" "rabbit")
(count (take 5000 (repeat "rabbit")))
;; -> 5000
```
We can use `rand-int` to generate a random int between 0 and 10:

``` clojure
(rand-int 10)
;; -> 3
(rand-int 10)
;; -> 4
```

``` clojure
#(rand-int 10)
;; -> #<user$eval721$fn__722 user$eval721$fn__722@308092db>
(#(rand-int 10))
;; -> 3

(repeatedly 5 #(rand-int 10))
;; -> (1 5 8 4 3)
(take 10 (repeatedly #(rand-int 10)))
;; -> (9 9 5 8 3 1 0 9 3 2)
```

``` clojure
(take 3 (cycle ["big" "small"]))
;; -> ("big" "small" "big")
(take 6 (cycle ["big" "small"]))
;; -> ("big" "small" "big" "small" "big" "small")

(take 3 (rest (cycle ["big" "small"])))
;; -> ("small" "big" "small")
```

# Recursion

``` clojure
(def adjs ["normal"
	"too small"
	"too big"
	"is swimming"])

(defn alice-is [input]
	(loop [in input
		out []]
		(if (empty? in)
			out
			(recur (rest in)
				(conj out
				(str "Alice is " (first in)))))))

(alice-is adjs)
;; -> ["Alice is normal" "Alice is too small" "Alice is too big" "Alice is is swimming"]
```

``` clojure
(defn countdown [n]
	(if (= n 0)
		n
		(recur (- n 1))))

(countdown 100000)
;; -> 0

```

# The Functional Shape of Data Transformations
## Map the Ultimate

``` clojure
(def animals [:mouse :duck :dodo :lory :eaglet])

(#(str %) :mouse)
;; -> ":mouse"

(map #(str %) animals)
;; -> (":mouse" ":duck" ":dodo" ":lory" ":eaglet")

(class (map #(str %) animals))
;; -> clojure.lang.LazySeq

(take 3 (map #(str %) (range)))
;; -> ("0" "1" "2")
(take 10 (map #(str %) (range)))
;; -> ("0" "1" "2" "3" "4" "5" "6" "7" "8" "9")

```

``` clojure
(println "Look at the mouse!")
;; Look at the mouse!
;; -> nil

(def animal-print (map #(println %) animals))
;; -> #'user/animal-print

animal-print
;; mouse
;; :duck
;; :dodo
;; :lory
;; :eaglet
;; -> (nil nil nil nil nil)

(def animal-print (doall (map #(println %) animals)))
;; mouse
;; :duck
;; :dodo
;; :lory
;; :eaglet
;; -> #'user/animal-print
animal-print
;; -> (nil nil nil nil nil)

```

``` clojure
(def animals
	["mouse" "duck" "dodo" "lory" "eaglet"])
(def colors
	["brown" "black" "blue" "pink" "gold"])
(defn gen-animal-string [animal color]
	(str color "-" animal))

(map gen-animal-string animals colors)
;; -> ("brown-mouse" "black-duck"
;; "blue-dodo" "pink-lory" "gold-eaglet")

```

``` clojure
(def animals
	["mouse" "duck" "dodo" "lory" "eaglet"])
(def colors
	["brown" "black"])

(map gen-animal-string animals colors)
;; -> ("brown-mouse" "black-duck")


(def animals
	["mouse" "duck" "dodo" "lory" "eaglet"])

(map gen-animal-string animals (cycle ["brown" "black"]))
;; -> ("brown-mouse" "black-duck" "brown-dodo"
;; "black-lory" "brown-eaglet")

```

## Reduce the Ultimate 

``` clojure
(reduce + [1 2 3 4 5])
;; -> 15

(reduce (fn [r x] (+ r (* x x))) [1 2 3])
;; -> 14

(reduce (fn [r x] (if (nil? x) r (conj r x)))
	[]
	[:mouse nil :duck nil nil :lory])
;; -> [:mouse :duck :lory]
```
## Other Useful Data Shaping Expressions

``` clojure
((complement nil?) nil)
;; -> false
((complement nil?) 1)
;; -> true

(filter (complement nil?) [:mouse nil :duck nil])
;; -> (:mouse :duck)

(filter keyword? [:mouse nil :duck nil])
;; -> (:mouse :duck)

(remove nil? [:mouse nil :duck nil])
;; -> (:mouse :duck)

```

``` clojure
(for [animal [:mouse :duck :lory]]
	(str (name animal)))
;; -> ("mouse" "duck" "lory")

(for [animal [:mouse :duck :lory]
		color [:red :blue]]
	(str (name color) (name animal)))
;; -> ("redmouse" "bluemouse"
;; "redduck" "blueduck"
;; "redlory" "bluelory")

```

``` clojure
(for [animal [:mouse :duck :lory]
	color [:red :blue]
	:let [animal-str (str "animal-"(name animal))
		color-str (str "color-"(name color))
		display-str (str animal-str "-" color-str)]]
	display-str)
;; -> ("animal-mouse-color-red" "animal-mouse-color-blue"
;; "animal-duck-color-red" "animal-duck-color-blue"
;; "animal-lory-color-red" "animal-lory-color-blue")

```

``` clojure
(for [animal [:mouse :duck :lory]
	color [:red :blue]
	:let [animal-str (str "animal-"(name animal))
		color-str (str "color-"(name color))
		display-str (str animal-str "-" color-str)]
		:when (= color :blue)]
	display-str)
;; -> ("animal-mouse-color-blue"
;; "animal-duck-color-blue"
;; "animal-lory-color-blue")

```

``` clojure
(flatten [ [:duck [:mouse] [[:lory]]]])
;; -> (:duck :mouse :lory)

(vec '(1 2 3))
;; -> [1 2 3]
(into [] '(1 2 3))
;; -> [1 2 3]

(sorted-map :b 2 :a 1 :z 3)
;; -> {:a 1, :b 2, :z 3}

(into (sorted-map) {:b 2 :c 3 :a 1})
;; -> {:a 1, :b 2, :c 3}

(into {} [[:a 1] [:b 2] [:c 3]])
;; -> {:a 1, :b 2, :c 3}

(into [] {:a 1, :b 2, :c 3})
;; -> [[:c 3] [:b 2] [:a 1]]

(partition 3 [1 2 3 4 5 6 7 8 9])
;; -> ((1 2 3) (4 5 6) (7 8 9))

(partition 3 [1 2 3 4 5 6 7 8 9 10])
;; -> ((1 2 3) (4 5 6) (7 8 9))

(partition-all 3 [1 2 3 4 5 6 7 8 9 10])
;; -> ((1 2 3) (4 5 6) (7 8 9) (10))

(partition-by #(= 6 %) [1 2 3 4 5 6 7 8 9 10])
;; -> ((1 2 3 4 5) (6) (7 8 9 10))

```
