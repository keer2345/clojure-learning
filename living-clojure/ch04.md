**Chapter 04 Java Interop and Polymorphism**


<!-- vim-markdown-toc GFM -->

* [Java的互操作](#java的互操作)
* [多态Polymorphism](#多态polymorphism)

<!-- vim-markdown-toc -->

# Java的互操作

调用方法：
```clojure
(class "caterpillar")

(. "caterpillar" toUpperCase)
;; -> "CATERPILLAR"

(.toUpperCase "caterpillar")
;; -> "CATERPILLAR"

(.indexOf "caterpillar" "pillar")
;; -> 5
```

创建对象：
```clojure
(new String "Hi!!")
;; -> "Hi!!"

(String. "Hi!!")
;; -> "Hi!!"
```
导入 Java 库，比如 `java.net.InetAddress`：

```clojure
(ns caterpillar.network
    (:import (java.net InetAddress)))

(InetAddress/getByName "localhost")
;; -> #<Inet4Address localhost/127.0.0.1>

(.getHostName (InetAddress/getByName "localhost"))
;; -> "localhost"
```
我们也可以不通过 `import` 来使用 Java 类：
```clojure
(java.net.InetAddress/getByName "localhost")
;; -> #<Inet4Address localhost/127.0.0.1>
```

也可以使用 `doto` macro，我们拿 `StringBuffer` 举例：
````clojure
(def sb (doto (StringBuffer. "Who ")
    (.append "are ")
    (.append "you?")))

(.toString sb)
;; -> "Who are you?"
````
`doto` 语法比起内置的语法更加易于阅读：
```clojure
(def sb
    (.append
        (.append
            (StringBuffer. "Who ")
            "are ")
            "you?"))

(.toString sb)
;; -> "Who are you?"
```

下表展示了 Java 和 Clojure 的使用对比：

**Java**|**Clojure**
----|----
`"caterpillar".toUpperCase();`|`(.toUpperCase "caterpillar")`
`"caterpillar".indexOf("pillar");`|`(.indexOf "caterpillar" "pillar")`
`new String("Hi!!");`|`(new String "Hi!!")`
`new String("Hi!!");`|`(String. "Hi!!")`
`InetAddress.getByName("localhost");`|`(InetAddress/getByName "localhost")`
`host.getHostName();`| `(.getHostName host)`

再举个例子：
```clojure
(import 'java.util.UUID)
(UUID/randomUUID)
;; -> #uuid "f9877259-2cc1-4e5a-8c6f-8b51499cb9f8"
```
- `import` UUID 的 Java 类
- 调用其方法

接下来学习多态（`polymorphism`）。

# 多态Polymorphism
如果我们想让函数实现不同类型的输入，可以使用 `case`。下面的例子使用 `cond`：
```clojure
(defn who-are-you
  [input]
  (cond
    (= java.lang.String (class input))     "String - Who are you?"
    (= clojure.lang.Keyword (class input)) "Keyword - Who are you?"
    (= java.lang.Long (class input))       "Number - Who are you?"))


ch04.core> (who-are-you :alice)
;; => "Keyword - Who are you?"
ch04.core> (who-are-you "alice")
;; => "String - Who are you?"
ch04.core> (who-are-you 123)
;; => "Number - Who are you?"
ch04.core> (who-are-you true)
;; => nil
ch04.core> (who-are-you '(1 2 3))
;; => nil
```

我们可以在 Clojure 中使用 multimethods 表达多态：
```clojure
(defmulti who-are-you-2 class)

(defmethod who-are-you-2 java.lang.String [input]
  (str "String - Who are you 2? " input))

(defmethod who-are-you-2 clojure.lang.Keyword [input]
  (str "Keyword - Who are you 2? " input))

(defmethod who-are-you-2 java.lang.Long [input]
  (str "Number - Who are you 2? " input))

ch04.core> (who-are-you-2 :alice)
;; => "Keyword - Who are you 2? :alice"
ch04.core> (who-are-you-2 "alice")
;; => "String - Who are you 2? alice"
ch04.core> (who-are-you-2 123)
;; => "Number - Who are you 2? 123"
ch04.core> (who-are-you-2 false)
Execution error (IllegalArgumentException) at ch04.core/eval17046 (form-init5260558242073116574.clj:65).
No method in multimethod 'who-are-you-2' for dispatch value: class java.lang.Boolean
ch04.core> (who-are-you-2 '(1 2 3))
Execution error (IllegalArgumentException) at ch04.core/eval17048 (form-init5260558242073116574.clj:68).
No method in multimethod 'who-are-you-2' for dispatch value: class clojure.lang.PersistentList
```

我们还可以使用 `:default` 分配默认方法，如果没有匹配到的话这个方法会取代异常：
```clojure
(defmethod who-are-you-2 :default [input]
  (str "I don't konw - who are you? " input))

(who-are-you-2 true)
;; => "I don't konw - who are you? true"
```
在前面的例子中，分配函数 `dispatch` 首先被调用，输入的是类，接着使用值来决定调用什么方法。

实际上，任何函数都可以用于分配。我们甚至可以通过观察输入的 map 值：
```clojure
(defmulti eat-mushroom #(if (< % 3) :grow :shrink))

(defmethod eat-mushroom :grow [_]
  "Eat the right side to grow.")

(defmethod eat-mushroom :shrink [_]
  "Eat the left side to shrink.")

(eat-mushroom 1)
;; -> "Eat the right side to grow."

(eat-mushroom 9)
;; -> "Eat the left side to shrink."
```


Clojure 多态的另一种方式是使用 `protocols`。`multimethod` 对于一个函数的时候能很好的使用多态，但有时 `protocols` 能更为优雅的处理一组函数的多态。我们来看 `eat-mushroom` 采用 `String`, `Keyword` 和 `Long` 时的多态。

首先定义 `protocol`：
```clojure
(defprotocol BigMushroom
  (eat-mushroom-2 [this]))

(extend-protocol BigMushroom
  java.lang.String
  (eat-mushroom-2 [this]
    (str (.toUpperCase this) " mmmm tasty!"))
  clojure.lang.Keyword
  (eat-mushroom-2 [this]
    (case this
      :grow   "Eat grow"
      :shrink "Eat shrink"))
  java.lang.Long
  (eat-mushroom-2 [this]
    (if (< this 8)
      "Eat less than 8"
      "Eat more than 8")))

ch04.core> (eat-mushroom-2 "Big Mushroom")
;; => "BIG MUSHROOM mmmm tasty!"
ch04.core> (eat-mushroom-2 :grow)
;; => "Eat grow"
ch04.core> (eat-mushroom-2 1)
;; => "Eat less than 8"
```

我们使用了内置的数据类型，如果我们想要添加自定义的类型呢？有两种解决方案。一种是用 `defrecord` 创建新的类型。

```clojure
(defrecord Mushroom [color height])
```

```clojure
ch04.core> (def regular-mushroom (Mushroom. "white" "2 inches"))
;; => #'ch04.core/regular-mushroo
ch04.core> (class regular-mushroom)
;; => ch04.core.Mushroom
ch04.core> (.-color regular-mushroom)
;; => "white"
ch04.core> (.-height regular-mushroom)
;; => "2 inches"
```
protocols 提供给我们的 `defrecord` 实现借口，可以结合数据结构和类型。我们定义 `Edible` 的 protocol，它有两个函数：`bite-right-side` 和 `bite-left-side`，都带有一个参数：
```clojure
(defprotocol Edible
  (bite-right-side [this])
  (bite-left-side [this]))
```
现在我们定义了 protocol，可以实现 records 了：
```clojure
(defrecord WonderlandMushroom [color height]
  Edible
  (bite-right-side [this]
    (str "The " color " bite make you grow bigger"))
  (bite-left-side [this]
    (str "The " color " bite makes you grow smaller")))
```
接下来，定义 `RegularMushroom` record ：
```clojure
(defrecord RegularMushroom [color height]
  Edible
  (bite-right-side [this]
    (str "The " color " bite tastes bad"))
  (bite-left-side [this]
    (str "The " color " bite tastes bad too")))
```
最后，构建我们的 mushrooms ：
```clojure
(def alice-mushroom (WonderlandMushroom. "blue dots" "3 inches"))
(def reg-mushroom (RegularMushroom. "brown" "1 inches"))

(bite-right-side alice-mushroom)
;; -> "The blue dots bite makes you grow bigger"
(bite-left-side alice-mushroom)
;; -> "The blue dots bite makes you grow smaller"

(bite-right-side reg-mushroom)
;; -> "The brown bite tastes bad"
(bite-left-side reg-mushroom)
;; -> "The brown bite tastes bad too"
```
